plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'checkstyle'
    id("io.github.gradle-nexus.publish-plugin") version "1.3.0"
}

group = 'ch.geowerkstatt.ilivalidator.extensions.functions'
archivesBaseName = 'interlis-functions-ngk-so'

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

repositories {
    mavenCentral()
    maven { url "https://jars.interlis.ch" }
}

dependencies {
    implementation group: 'ch.interlis', name: 'iox-api', version: '1.0.4'
    implementation group: 'ch.interlis', name: 'iox-ili', version: '1.22.0'
    implementation group: 'ch.interlis', name: 'ili2c-tool', version: '5.4.0'
    implementation group: 'ch.interlis', name: 'ili2c-core', version: '5.4.0'
    implementation group: 'com.vividsolutions', name: 'jts-core', version: '1.14.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'
}

java{
    withSourcesJar()
    withJavadocJar()
}

test {
    useJUnitPlatform()
}

publishing {
    publications{
        InterlisCustomFunctions(MavenPublication){
            from components.java
            pom {
                name = 'interlis-functions-ngk-so'
                description = 'Library containing function implementations for ilivalidator / INTERLIS v2.4'
                url = 'https://github.com/GeoWerkstatt/interlis-functions-ngk-so'
                licenses {
                    license {
                        name = 'GNU Lesser General Public License v2.1'
                        url = 'https://github.com/GeoWerkstatt/interlis-functions-ngk-so/blob/main/LICENSE.md'
                    }
                }
                developers {
                    developer {
                        id = 'GeoWerkstatt'
                        name = 'GeoWerkstatt GmbH'
                        email = 'office@geowerkstatt.ch'
                    }
                }
                scm {
                    connection = 'scm:git:git:https://github.com/GeoWerkstatt/interlis-functions-ngk-so.git'
                    developerConnection = 'scm:git:git:https://github.com/GeoWerkstatt/interlis-functions-ngk-so.git'
                    url = 'https://github.com/GeoWerkstatt/interlis-functions-ngk-so/'
                }
            }
        }
    }

    repositories {
        maven {
            name = 'GitHubPackages'
            url = uri('https://maven.pkg.github.com/GeoWerkstatt/interlis-functions-ngk-so')
            credentials {
                username = findProperty("GITHUB_ACTOR")
                password = findProperty("GITHUB_TOKEN")
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype { //only for users registered in Sonatype after 24 Feb 2021
            username = findProperty("OSSRH_USER")
            password = findProperty("OSSRH_PASSWORD")
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}

signing{
    def key = findProperty("SIGNING_KEY")
    def password = findProperty("SIGNING_PASSWORD")

    useInMemoryPgpKeys(key, password)
    sign(publishing.publications.InterlisCustomFunctions)
}